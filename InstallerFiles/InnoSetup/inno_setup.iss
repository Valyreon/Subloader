; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Subloader"
#define MyAppVersion "1.6"
#define MyAppPublisher "Valyreon"
#define MyAppURL "https://github.com/Valyreon/Subloader"
#define MyAppExeName "SubLoad.exe"
#define MyAppCliExeName "subloader-cli.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{F15A11C1-B5EA-423C-8EA4-3FD462C0C30F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=licence.txt
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=admin
OutputBaseFilename=SubloaderV160
Compression=lzma
SolidCompression=yes
WizardStyle=modern
ArchitecturesInstallIn64BitMode=x64
SetupIconFile=icon.ico

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "context_menu"; Description: "Add 'Find subtitles' to context menu of .avi, .mp4 and .mkv files"; GroupDescription: "{cm:AdditionalIcons}";

[InstallDelete]
Type: files; Name: "{app}\Uninstall.exe"
Type: filesandordirs; Name: "{pf64}/Subloader"
Type: filesandordirs; Name: "{commonappdata}/Roaming/Microsoft/Windows/Start Menu/Programs/Subloader"
Type: filesandordirs; Name: "{commonappdata}/Microsoft/Windows/Start Menu/Programs/Subloader"
Type: filesandordirs; Name: "{userappdata}/Roaming/Microsoft/Windows/Start Menu/Programs/Subloader"
Type: filesandordirs; Name: "{userappdata}/Microsoft/Windows/Start Menu/Programs/Subloader"

[Files]
Source: "{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#MyAppCliExeName}"; DestDir: "{app}"; Flags: ignoreversion; Components: cli
Source: "remove_path.ps1"; DestDir: "{app}/UninstallerScripts"; Flags: ignoreversion; Components: cli
Source: "set_path.ps1"; DestDir: "{tmp}"; Flags: ignoreversion; Components: cli

[Registry]
Root: HKLM; Subkey: "Software\Classes\Subloader.avi"; Flags: uninsdeletekey deletekey
Root: HKLM; Subkey: "Software\Classes\Subloader.avi\shell\open"; ValueType: string; ValueName: "FriendlyAppName"; ValueData: "Subloader";
Root: HKLM; Subkey: "Software\Classes\Subloader.avi\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1""";
Root: HKLM; Subkey: "Software\Classes\.avi\OpenWithProgIds"; ValueType: string; ValueName: "Subloader.avi";

Root: HKLM; Subkey: "Software\Classes\Subloader.mp4"; Flags: uninsdeletekey deletekey
Root: HKLM; Subkey: "Software\Classes\Subloader.mp4\shell\open"; ValueType: string; ValueName: "FriendlyAppName"; ValueData: "Subloader";
Root: HKLM; Subkey: "Software\Classes\Subloader.mp4\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1""";
Root: HKLM; Subkey: "Software\Classes\.mp4\OpenWithProgIds"; ValueType: string; ValueName: "Subloader.mp4";

Root: HKLM; Subkey: "Software\Classes\Subloader.mkv"; Flags: uninsdeletekey deletekey
Root: HKLM; Subkey: "Software\Classes\Subloader.mkv\shell\open"; ValueType: string; ValueName: "FriendlyAppName"; ValueData: "Subloader";
Root: HKLM; Subkey: "Software\Classes\Subloader.mkv\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1""";
Root: HKLM; Subkey: "Software\Classes\.mkv\OpenWithProgIds"; ValueType: string; ValueName: "Subloader.mkv";

; clean old registry items from old installers
Root: HKCR; Subkey: "SystemFileAssociations\.avi\shell\Subloader"; Flags: uninsdeletekey deletekey;
Root: HKCR; Subkey: "SystemFileAssociations\.avi\shell\Subloader"; Flags: uninsdeletekey deletekey;
Root: HKCR; Subkey: "SystemFileAssociations\.mkv\shell\Subloader"; Flags: uninsdeletekey deletekey;
Root: HKLM; Subkey: "Software\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\Subloader"; Flags: uninsdeletekey deletekey;

Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.avi\shell\Subloader"; Flags: uninsdeletekey deletekey; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.avi\shell\Subloader"; ValueType: string; ValueName: ""; ValueData: "Find subtitles"; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.avi\shell\Subloader"; ValueType: string; ValueName: "Icon"; ValueData: "{app}\{#MyAppExeName}"; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.avi\shell\Subloader\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Check: IsContextMenuTaskChecked

Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Subloader"; Flags: uninsdeletekey deletekey; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Subloader"; ValueType: string; ValueName: ""; ValueData: "Find subtitles"; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Subloader"; ValueType: string; ValueName: "Icon"; ValueData: "{app}\{#MyAppExeName}"; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Subloader\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Check: IsContextMenuTaskChecked

Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.mkv\shell\Subloader"; Flags: uninsdeletekey deletekey; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.mkv\shell\Subloader"; ValueType: string; ValueName: ""; ValueData: "Find subtitles"; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.mkv\shell\Subloader"; ValueType: string; ValueName: "Icon"; ValueData: "{app}\{#MyAppExeName}"; Check: IsContextMenuTaskChecked
Root: HKLM; Subkey: "Software\Classes\SystemFileAssociations\.mkv\shell\Subloader\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Check: IsContextMenuTaskChecked

[Components]
Name: "main"; Description: "Main Files"; Types: full compact custom; Flags: fixed
; Name: "context_menu"; Description: "Add to context menu (.avi, .mp4 and .mkv files)"; Types: full compact
Name: "cli"; Description: "Command Line Interface (Installs CLI executable and adds it to Path env variable)"; Types: full

[Run]
Filename: "powershell.exe"; Parameters: "-ExecutionPolicy Bypass -File ""{tmp}/set_path.ps1"" ""{app}"" "; Flags: runhidden; Components: cli

[UninstallRun]
Filename: "powershell.exe"; Parameters: "-ExecutionPolicy Bypass -File ""{app}/UninstallerScripts/remove_path.ps1"" ""{app}"" "; Flags: runhidden; Components: cli; RunOnceId: remove_path

[UninstallDelete]
Type: filesandordirs; Name: "{userappdata}\Subloader"

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Code]
function IsContextMenuTaskChecked: Boolean;
begin
  Result := WizardIsTaskSelected('context_menu');
end;